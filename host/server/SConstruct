import os

env = Environment()

extLibsDir = '../../external-libs/'

env.Append(CPPFLAGS = Split('-std=c++11 -Wall'))
env.Append(CPPPATH = [Dir('../../firmware/common'), Dir(extLibsDir + '/wallaroo/')])

# libraries
libraries = 'libusb-1.0 log4cpp jsoncpp'
librariesNoPkgConfig = 'boost_system'

for lib in Split(libraries):
	env.ParseConfig('pkg-config %s --cflags --libs' % lib)
env['LIBS'].append(Split(librariesNoPkgConfig))

binary_name = 'szark_server'
build_dir = 'build'

env["CXX"] = "clang++"
env['ENV']['TERM'] = os.environ['TERM']

env.VariantDir(build_dir + '/src', 'src')
env.VariantDir(build_dir + '/test', 'test')

commonObjectFiles = env.Object([f for f in Glob(build_dir + '/src/*.cpp') if 'main.cpp' not in f.path])

env.Program(build_dir + '/' + binary_name, commonObjectFiles + [build_dir + '/src/main.cpp'])

testEnv = env.Clone()
testEnv['CPPPATH'].append('./test/')
testEnv['CPPPATH'].append('./src/')
testEnv['LIBS'].append('boost_unit_test_framework')

# here I append all needed object files
testObjectFiles = Glob(build_dir + '/test/*.cpp')
testEnv.Program(build_dir + '/' + binary_name + '_test', testObjectFiles + commonObjectFiles)

# vim: set ft=python
